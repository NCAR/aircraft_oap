#!python

env = Environment(tools = ['default', 'openmotif', 'raf'])


env.Append(CPPPATH='class')
env.Append(LIBPATH=["../lib"])
env['PUBLISH_PREFIX'] = '/net/www/docs/raf/Software'

env.Append(CXXFLAGS='-std=c++11 -g -Wall -Wno-write-strings -Wno-deprecated-register')

env.Append(CPPDEFINES=['PNG'])

# Change default fonts for Ubuntu as the adobe courier is not available.
#env.Append(CPPDEFINES=['UBUNTU'])

env.Append(LIBS=['png'])
env.Append(LIBS=['z'])

sources = Split("""
class/CanvasWindow.cc
class/Colors.cc
class/ControlWindow.cc
class/DataFile.cc
class/Enchilada.cc
class/FileMgr.cc
class/Hex.cc
class/Histogram.cc
class/Magnify.cc
class/MagnifyCanvas.cc
class/MagnifyWindow.cc
class/MainCanvas.cc
src/cb_canvas.cc
src/cb_control.cc
src/cb_mag.cc
src/cb_menus.cc
src/cb_printer.cc
src/cb_text.cc
src/ccb.cc
src/global.cc
src/xpms2d.cc
""")

## src/process.cc

xpms2d = env.Program(target = 'src/xpms2d', source = sources)
env.Default(xpms2d)

env.Install("$INSTALL_PREFIX/bin", 'src/xpms2d')

if "publish" in COMMAND_LINE_TARGETS:
   pub = env.Install('$PUBLISH_PREFIX', ["src/xpms2d.html"])
   env.Alias('publish', pub)
